plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
}

version project_version
group project_group

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.layered {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-${parchment_minecraft_version}:${parchment_version}@zip"
	}

	modImplementation "net.fabricmc:fabric-loader:${loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${fabric_kotlin_version}"
}

loom {
	splitEnvironmentSourceSets()

	mixin {
		useLegacyMixinAp = false
	}
}

jar {
	inputs.property('name', name)

	from('LICENSE') {
		rename { "${it}_${inputs.properties.name}" }
	}
}

processResources {
	inputs.properties(
			'version': version,
			'loader': loader_version,
			'fabric': fabric_version,
			'fk': fabric_kotlin_version
	)

	filesMatching('fabric.mod.json') {
		expand(inputs.properties) {
			escapeBackslash = true
		}
	}
}